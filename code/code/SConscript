# hey emacs, this is -*- python -*-

Import('env');

twitter = env.get('TWITTER', True)
cxxflags = env.get('CXXFLAGS', '')
if twitter:
  cxxflags.append('-DTWITTER')
makelib = env.StaticLibrary if env.get('LIBTYPE', '') == 'static' else env.SharedLibrary


# library targets
libcmd=makelib(target='cmd', source=Glob('cmd/*.cc'))
libdisc=makelib(target='disc', source=Glob('disc/*.cc'))
libgame=makelib(target='game', source=Glob('game/*.cc'))
libmisc=makelib(target='misc', source=Glob('misc/*.cc'), LIBS=['curl'])
libobj=makelib(target='obj', source=Glob('obj/*.cc'))
libspec=makelib(target='spec', source=Glob('spec/*.cc'))
libsys=makelib(target='sys', source=Glob('sys/*.cc'), LIBS=['crypt'], CXXFLAGS=cxxflags)
libtask=makelib(target='task', source=Glob('task/*.cc'))

libs=[libsys, libmisc, libcmd, libdisc, libgame,
      libobj, libspec, libtask]

if twitter:
  libs.append('curl')

# main binary
sneezy=env.Program('sneezy', 'main.cc', LIBS=libs + ['cares', 'boost_program_options', 'mysqlclient', 'boost_regex'])
env.InstallAs('../sneezy', sneezy)

# low tools
lto=env.Object('low/lowtools.cc');

amap=env.Program(['low/amap.cc', lto], LIBS=libs)
sqladdmobs=env.Program(['low/sqladdmobs.cc', lto], LIBS=libs)
sqladdobjs=env.Program(['low/sqladdobjs.cc', lto], LIBS=libs)
sqladdresp=env.Program(['low/sqladdresp.cc', lto], LIBS=libs)
sqladdwld=env.Program(['low/sqladdwld.cc', lto], LIBS=libs)
sqledimmobj=env.Program(['low/sqledimmobj.cc', lto], LIBS=libs)
sqledmob=env.Program(['low/sqledmob.cc', lto], LIBS=libs)
sqledobj=env.Program(['low/sqledobj.cc', lto], LIBS=libs)
sqledresp=env.Program(['low/sqledresp.cc', lto], LIBS=libs)
sqledshop=env.Program(['low/sqledshop.cc', lto], LIBS=libs)
sqledwld=env.Program(['low/sqledwld.cc', lto], LIBS=libs)

env.Install('../lowtools', [amap, sqladdmobs, sqladdobjs, sqladdresp,
                            sqladdwld, sqledimmobj, sqledmob, sqledobj,
                            sqledresp, sqledshop, sqledwld])
env.Alias('lowtools', '../lowtools')

# cgi
cgi=env.Object('cgi/session.cgi.cc')

mudmail=env.Program(['cgi/mudmail.cgi.cc', cgi], LIBS=libs)

env.Install('../cgi', [mudmail])
env.Alias('cgi', '../cgi')

# shell tools
econ=env.Program(['shell/econ.cc'], LIBS=libs)

env.Install('../shell', [econ])
env.Alias('shell', '../shell')


# unit tests
env.SConscript('tests/SConscript',
               exports='libs')
